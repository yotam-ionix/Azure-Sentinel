id: 51643a38-679c-4989-9341-2c01a2bf478c
Function:
  Title: Parser for PaloAltoCDLEvent
  Version: '1.0.0'
  LastUpdated: '2023-08-23'
Category: Microsoft Sentinel Parser
FunctionName: PaloAltoCDLEvent
FunctionAlias: PaloAltoCDLEvent
FunctionQuery: |
    CommonSecurityLog
    | where DeviceVendor =~ 'Palo Alto Networks'
    | where DeviceProduct =~ 'LF'
    | extend EventVendor = 'Palo Alto Networks'
    | extend EventProduct = 'Cortex Data Lake'
    | extend EventSchemaVersion = 0.2
    | extend EventCount = 1
    | extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
             DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
             DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3)
    | extend packed = pack(DeviceCustomNumber1Label, DeviceCustomNumber1
                         , DeviceCustomNumber2Label, DeviceCustomNumber2
                         , DeviceCustomNumber3Label, DeviceCustomNumber3
                         , DeviceCustomString1Label, DeviceCustomString1
                         , DeviceCustomString2Label, DeviceCustomString2
                         , DeviceCustomString3Label, DeviceCustomString3
                         , DeviceCustomString4Label, DeviceCustomString4
                         , DeviceCustomString5Label, DeviceCustomString5
                         , DeviceCustomString6Label, DeviceCustomString6
                         , DeviceCustomDate1Label, DeviceCustomDate1
                         , DeviceCustomDate2Label, DeviceCustomDate2
                         , FlexString1Label, FlexString1
                         , FlexString2Label, FlexString2
                         , DeviceCustomFloatingPoint1Label, DeviceCustomFloatingPoint1
                         , DeviceCustomFloatingPoint2Label, DeviceCustomFloatingPoint2
                         , DeviceCustomIPv6Address1Label, DeviceCustomIPv6Address1
                         , DeviceCustomIPv6Address2Label, DeviceCustomIPv6Address2
                         , DeviceCustomIPv6Address3Label, DeviceCustomIPv6Address3)
    | evaluate bag_unpack(packed)
    | mv-apply AdditionalFields = extract_all(@'(?P<key>[a-zA-Z0-9-_]+)=(?P<value>[a-zA-Z0-9-_:@.,?%#(){}><\/"\\ ]+)', dynamic(["key","value"]), AdditionalExtensions) on (
        project packed1 = pack(tostring(AdditionalFields[0]), tostring(AdditionalFields[1]))
        | summarize bag = make_bag(packed1)
    )
    | evaluate bag_unpack(bag)
    | extend DvcIpAddr = column_ifexists( "Device IPv6 Address" , "")
             , DstIpAddr = column_ifexists( "Destination IPv6 Address" , "")
             , SrcIpAddr = column_ifexists( "Source IPv6 Address" , "")
             , EventResultDetails = coalesce(column_ifexists("reason",""),column_ifexists("Reason",""))
             , SrcZone = column_ifexists( "FromZone" , "")
             , DstZone = column_ifexists( "Zone" , "")
             , NetworkPackets = column_ifexists( "PacketsTotal" , int(null))
             , NetworkDuration = column_ifexists( "SessionDuration" , int(null))
             , NetworkSessionId = column_ifexists( "SessionID" , int(null))
             , EventStartTime = coalesce(column_ifexists("StartTime",datetime(null))
             , todatetime(column_ifexists("start","")))
             , EventEndTime = coalesce(column_ifexists("EventEndTime",datetime(null))
             , todatetime(column_ifexists("end","")))
             , EventType = coalesce(column_ifexists("DeviceEventCategory",""), column_ifexists("cat",""))
    | project-rename EventProductVersion = DeviceVersion
                    , DvcId = DeviceExternalID
                    , DvcHostname = DeviceName
                    , DstNatPortNumber = DestinationTranslatedPort
                    , DstHostname = DestinationHostName
                    , SrcNatPortNumber = SourceTranslatedPort
                    , SrcFileName = FileName
                    , SrcFilePath = FilePath
                    , EventMessage = Message
                    , EventSeverity = LogSeverity
                    , EventResult = Activity
                    , DstPortNumber = DestinationPort
                    , DstUserId = DestinationUserID
                    , EventResourceId = DeviceEventClassID
                    , HttpRequestMethod = RequestMethod
                    , Url = RequestURL
                    , HttpContentFormat = RequestContext
                    , SrcHostname = SourceHostName
                    , DvcAction = DeviceAction
                    , DstDomain = DestinationNTDomain
                    , SrcPortNumber = SourcePort
                    , DvcInboundInterface = DeviceInboundInterface
                    , DvcOutboundInterface = DeviceOutboundInterface
                    , NetworkProtocol = Protocol
                    , NetworkApplicationProtocol = ApplicationProtocol
                    , SrcDomain = SourceNTDomain
                    , SrcUserId = SourceUserID
                    , DstBytes = ReceivedBytes
                    , SrcBytes = SentBytes
    | extend EventTimeIngested = todatetime(ReceiptTime)
    | extend SrcNatIpAddr = case(isempty(SourceIP), SourceTranslatedAddress, 
                                pack_array(SourceTranslatedAddress,SourceIP))
    | extend DstNatIpAddr = case(isempty(DestinationIP), DestinationTranslatedAddress,
                                pack_array(DestinationTranslatedAddress, DestinationIP))
    | extend suser0 = column_ifexists("suser0","")
           , duser0 = column_ifexists("duser0","")
     | extend SrcUsername = case(isempty(suser0), SourceUserName, 
                                 pack_array(SourceUserName,suser0))
     | extend DstUsername = case(isempty(duser0), DestinationUserName,
                                 pack_array(DestinationUserName,duser0))
    | project-away ReceiptTime
                  , Type
                  , StartTime
                  , EndTime
                  , DeviceVendor
                  , DeviceProduct
                  , duser0
                  , DestinationUserName
                  , suser0
                  , SourceUserName
                  , AdditionalExtensions
                  , DestinationTranslatedAddress
                  , DestinationIP
                  , SourceTranslatedAddress
                  , SourceIP
                  , DeviceCustomNumber1Label
                  , DeviceCustomNumber1
                  , DeviceCustomNumber2Label
                  , DeviceCustomNumber2
                  , DeviceCustomNumber3Label
                  , DeviceCustomNumber3
                  , DeviceCustomString1Label
                  , DeviceCustomString1
                  , DeviceCustomString2Label
                  , DeviceCustomString2
                  , DeviceCustomString3Label
                  , DeviceCustomString3
                  , DeviceCustomString4Label
                  , DeviceCustomString4
                  , DeviceCustomString5Label
                  , DeviceCustomString5
                  , DeviceCustomString6Label
                  , DeviceCustomString6
                  , DeviceCustomDate1Label
                  , DeviceCustomDate1
                  , DeviceCustomDate2Label
                  , DeviceCustomDate2
                  , FlexString1Label
                  , FlexString1
                  , FlexString2Label
                  , FlexString2
                  , DeviceCustomIPv6Address1Label
                  , DeviceCustomIPv6Address1
                  , DeviceCustomIPv6Address2Label
                  , DeviceCustomIPv6Address2
                  , DeviceCustomIPv6Address3Label
                  , DeviceCustomIPv6Address3
                  , DeviceCustomFloatingPoint1Label
                  , DeviceCustomFloatingPoint1
                  , DeviceCustomFloatingPoint2Label
                  , DeviceCustomFloatingPoint2